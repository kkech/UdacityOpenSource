apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'checkstyle'

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        applicationId Versions.appId
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.appVersionCode
        versionName Versions.appVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        lintConfig file("./quality/lint.xml")
    }

    // Required by DL4J
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/INDEX.LIST'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation MainApplicationDependencies.kotlin
    implementation MainApplicationDependencies.coroutines
    implementation MainApplicationDependencies.appCompat
    implementation MainApplicationDependencies.rxJava
    implementation MainApplicationDependencies.rxAndroid
    implementation(MainApplicationDependencies.workerManager) {
        exclude group: 'com.google.guava', module: 'listenablefuture' // Already in DL4J
    }

    implementation MainApplicationDependencies.multiDex
    implementation MainApplicationDependencies.archComponentLifeCycleExtensions
    implementation MainApplicationDependencies.archComponentLifeCycleViewModel

    // Data source dependencies
    implementation(DataSourceDependencies.socketIO) {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }
    implementation DataSourceDependencies.msgpack
    implementation DataSourceDependencies.lz4
    implementation DataSourceDependencies.okhttp

    // Tools
    implementation ToolsDependencies.npy

    // DL4J
    implementation(DL4JDependencies.dl4j) {
        exclude group: 'org.bytedeco', module: 'opencv-platform'
        exclude group: 'org.bytedeco', module: 'leptonica-platform'
        exclude group: 'org.bytedeco', module: 'hdf5-platform'
        exclude group: 'org.nd4j', module: 'nd4j-base64'
    }
    implementation DL4JDependencies.nd4jNative
    implementation DL4JDependencies.nd4jNativeArm
    implementation DL4JDependencies.nd4jNativeArm64
    implementation DL4JDependencies.nd4jNativeX86
    implementation DL4JDependencies.nd4jNativeX86_64

    implementation DL4JDependencies.openblas
    implementation DL4JDependencies.openblasAndroidArm
    implementation DL4JDependencies.openblasAndroidArm64
    implementation DL4JDependencies.openblasAndroidX86
    implementation DL4JDependencies.openblasAndroidX86_64

    implementation DL4JDependencies.opencv
    implementation DL4JDependencies.opencvAndroidArm
    implementation DL4JDependencies.opencvAndroidArm64
    implementation DL4JDependencies.opencvAndroidX86
    implementation DL4JDependencies.opencvAndroidX86_64

    implementation DL4JDependencies.leptonica
    implementation DL4JDependencies.leptonicaAndroidArm
    implementation DL4JDependencies.leptonicaAndroidArm64
    implementation DL4JDependencies.leptonicaAndroidX86
    implementation DL4JDependencies.leptonicaAndroidX86_64

    implementation DL4JDependencies.gson
    annotationProcessor DL4JDependencies.lombok

    implementation 'com.google.code.findbugs:annotations:3.0.1', {
        exclude module: 'jsr305'
        exclude module: 'jcip-annotations'
    }

    // Testing
    testImplementation UnitTestDependencies.junit
    testImplementation UnitTestDependencies.mockk

    androidTestImplementation InstrumentationDependencies.testRunner
    androidTestImplementation InstrumentationDependencies.espressoCore

    //This corrects for a junit version conflict.
    configurations.all {
        resolutionStrategy.force UnitTestDependencies.junit
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}

task staticAnalysis(type: GradleBuild) {
    description 'Run all static analysis tasks'
    tasks = ['ktlint']
}

check.dependsOn ktlint

dependencies {
    ktlint ToolsDependencies.ktlint
}

dependencies {
    implementation 'com.android.support:design:28.+'
    implementation 'com.android.support.constraint:constraint-layout:+'
}
